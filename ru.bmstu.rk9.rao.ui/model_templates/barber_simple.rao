enum Состояние_парикмахера {Свободен, Занят}

type Парикмахерские {
	Состояние_парикмахера состояние_парикмахера;
	int количество_в_очереди;
	int количество_обслуженных;
}

resource Парикмахерская = Парикмахерские.create(Состояние_парикмахера.Свободен, 0, 0);

event Событие_прихода_клиента() {
	Событие_прихода_клиента.plan(currentTime + Интервал_прихода.next());
	Парикмахерская.количество_в_очереди++;
}

operation Образец_обслуживания_клиента() {
	relevant _Парикмахерская = Парикмахерская.select(_Парикмахерская.состояние_парикмахера == Состояние_парикмахера.Свободен
			and _Парикмахерская.количество_в_очереди > 0);

	set duration() {
		return Длительность_обслуживания.next();
	}
	set begin() {
		_Парикмахерская.количество_в_очереди--;
		_Парикмахерская.состояние_парикмахера = Состояние_парикмахера.Занят;
	}
	set end() {
		_Парикмахерская.состояние_парикмахера = Состояние_парикмахера.Свободен;
		_Парикмахерская.количество_обслуженных++;
	}
}

dpt model {
	activity Обслуживание_клиента checks Образец_обслуживания_клиента();
}

sequence Интервал_прихода = double legacy exponential(123456789, 1/30.0);
sequence Длительность_обслуживания = double legacy uniform(123456789, 20, 40);

set init() {
	Событие_прихода_клиента.plan(currentTime + Интервал_прихода.next());
}

set terminateCondition() {
	return currentTime >= 7 * 12 * 60;
}

result Занятость_парикмахера = watchState(Парикмахерская.состояние_парикмахера == Состояние_парикмахера.Занят);
result Длина_очереди = watchParameter(Парикмахерская.количество_в_очереди);
result Всего_обслужено = getValue(Парикмахерская.количество_обслуженных);
result Пропускная_способность = getValue(Парикмахерская.количество_обслуженных / currentTime * 60);
result Длительность_работы = getValue(currentTime / 60);
