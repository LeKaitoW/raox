enum Тип_клиента {Тип1, Тип2}
enum Состояние_клиента {Пришел, Начал_стрижку}
enum Состояние_парикмахера {Свободен, Занят}

type Парикмахерские {
	int количество_в_очереди;
}

type Клиенты {
	Тип_клиента тип;
	Состояние_клиента состояние;
}

type Парикмахеры {
	Состояние_парикмахера состояние_парикмахера = Состояние_парикмахера.Свободен;
	int количество_обслуженных;
	int длительность_min;
	int длительность_max;
	Тип_клиента тип_клиента;
}

resource Парикмахерская = Парикмахерские.create(0);
resource Парикмахер_1 = Парикмахеры.create(*, 0, 20, 40, Тип_клиента.Тип1);
resource Парикмахер_2 = Парикмахеры.create(*, 0, 25, 70, Тип_клиента.Тип2);
resource Парикмахер_3 = Парикмахеры.create(*, 0, 30, 60, Тип_клиента.Тип2);


event Событие_прихода_клиента() {
	Клиенты.create(Случайный_тип_клиента.next(), Состояние_клиента.Пришел);
	Событие_прихода_клиента.plan(currentTime + Интервал_прихода.next());
	Парикмахерская.количество_в_очереди++;
}

operation Образец_обслуживания_клиента() {
	relevant _Парикмахерская = Парикмахерская.select(_Парикмахерская.количество_в_очереди > 0);
	relevant _Клиент = Клиенты.select(_Клиент.состояние == Состояние_клиента.Пришел);
	relevant _Парикмахер = Парикмахеры.select(_Парикмахер.состояние_парикмахера == Состояние_парикмахера.Свободен
			and _Парикмахер.тип_клиента == _Клиент.тип).withMin(_Парикмахер.количество_обслуженных);

	set duration() {
		return Длительность_обслуживания.next(_Парикмахер.длительность_min, _Парикмахер.длительность_max);
	}

	set begin() {
		_Парикмахерская.количество_в_очереди--;
		_Клиент.состояние = Состояние_клиента.Начал_стрижку;
		_Парикмахер.состояние_парикмахера = Состояние_парикмахера.Занят;
	}

	set end() {
		_Парикмахер.состояние_парикмахера = Состояние_парикмахера.Свободен;
		_Парикмахер.количество_обслуженных++;
		_Клиент.erase();
	}
}

dpt model {
	activity Обслуживание_клиента checks Образец_обслуживания_клиента();
}

sequence Интервал_прихода = double legacy exponential(123456789, 1/30.0);
sequence Длительность_обслуживания = double uniform(123456789);
sequence Случайный_тип_клиента = Тип_клиента histogram(123456789) {
	Тип_клиента.Тип1 1.0
	Тип_клиента.Тип2 5.0
}

set init() {
	Событие_прихода_клиента.plan(currentTime + Интервал_прихода.next());
}

set terminateCondition() {
	return currentTime >= 7 * 12 * 60;
}

result Занятость_парикмахера_1 = watchState(Парикмахер_1.состояние_парикмахера == Состояние_парикмахера.Занят);
result Занятость_парикмахера_2 = watchState(Парикмахер_2.состояние_парикмахера == Состояние_парикмахера.Занят);
result Занятость_парикмахера_3 = watchState(Парикмахер_3.состояние_парикмахера == Состояние_парикмахера.Занят);
result Обслужено_парикмахером_1 = getValue(Парикмахер_1.количество_обслуженных);
result Обслужено_парикмахером_2 = getValue(Парикмахер_2.количество_обслуженных);
result Обслужено_парикмахером_3 = getValue(Парикмахер_3.количество_обслуженных);
